plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'dev.architectury.loom' version '0.11.0-SNAPSHOT' apply false
    id 'org.cadixdev.licenser' version '0.6.1'
    id "org.ajoberstar.grgit" version "3.1.1"
    id 'com.matthewprenger.cursegradle' version "1.4.0"
}

def runNumber = (System.getenv("GITHUB_RUN_NUMBER") == null ? "9999" : System.getenv("GITHUB_RUN_NUMBER"))
version = rootProject.base_version + "." + runNumber

logger.lifecycle("Building cloth-api: " + version)
archivesBaseName = "cloth-api"

def getBranch() {
    if (System.getenv().GIT_BRANCH) {
        def branch = System.getenv().GIT_BRANCH
        return branch.substring(branch.lastIndexOf("/") + 1)
    }
    if (grgit == null) {
        return "unknown"
    }
    def branch = grgit.branch.current().name
    return branch.substring(branch.lastIndexOf("/") + 1)
}

allprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'org.cadixdev.licenser'

    sourceCompatibility = targetCompatibility = 1.8

    group = "me.shedaniel.cloth.api"

    ext {
        shouldGenerateData = false
    }

    sourceSets {
        testmod {
            compileClasspath += main.compileClasspath
            runtimeClasspath += main.runtimeClasspath
        }
        main {
            resources {
                srcDir 'src/generated/resources'
            }
        }
        datagen {
            compileClasspath += main.compileClasspath
            runtimeClasspath += main.runtimeClasspath
        }
    }

    repositories {
        maven { url "https://maven.shedaniel.me/" }
    }

    dependencies {
        minecraft "com.mojang:minecraft:$project.minecraft_version"
        mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
        modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
        modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"
        compileOnly "com.google.code.findbugs:jsr305:3.0.2"
        modApi "me.shedaniel.cloth:basic-math:${project.cloth_basic_math}"
    }

    configurations {
        dev
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release.set 16
    }

    artifacts {
        dev file: jar.archiveFile.get().asFile, type: "jar", builtBy: jar
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    license {
        header rootProject.file('HEADER')
        include '**/*.java'
    }

    afterEvaluate {
//        if (shouldGenerateData) {
//            task generateData(type: RunClientTask, dependsOn: downloadAssets) {
//                classpath = configurations.runtimeClasspath
//                classpath sourceSets.main.output
//                classpath sourceSets.datagen.output
//            }
//
//            build.dependsOn generateData
//        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

subprojects {
    version = rootProject.version

    dependencies {
        testmodImplementation sourceSets.main.output
        datagenImplementation sourceSets.main.output
    }

    publishing {
        publications {
            create("${archivesBaseName}_mavenJava", MavenPublication) {
                afterEvaluate {
                    artifact(remapJar)
                    artifact(sourcesJar) {
                        builtBy remapSourcesJar
                    }
                }
            }
        }

        repositories {
            if (System.getenv("MAVEN_PASS") != null) {
                maven {
                    url = "https://deploy.shedaniel.me/"
                    credentials {
                        username = "shedaniel"
                        password = System.getenv("MAVEN_PASS")
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar)
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
            pom.withXml {
                def depsNode = asNode().appendNode("dependencies")
                subprojects.each {
                    def depNode = depsNode.appendNode("dependency")
                    depNode.appendNode("groupId", it.group)
                    depNode.appendNode("artifactId", it.name)
                    depNode.appendNode("version", it.version)
                    depNode.appendNode("scope", "compile")
                }
            }
        }
    }

    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                url = "https://deploy.shedaniel.me/"
                credentials {
                    username = "shedaniel"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}

task licenseFormatAll
subprojects { p -> licenseFormatAll.dependsOn("${p.path}:licenseFormat") }
subprojects.each { remapJar.dependsOn("${it.path}:remapJar") }

sourceSets {
    testmod
    datagen
}

dependencies {
    subprojects.each {
        implementation project(path: ":${it.name}", configuration: "namedElements")
        include project("${it.name}:")

        testmodImplementation project("${it.name}:").sourceSets.testmod.output
    }

    include "me.shedaniel.cloth:basic-math:${project.cloth_basic_math}"
}

curseforge {
    apiKey = project.hasProperty('apiKey') ? project.property('apiKey') : System.getenv('CF_API_KEY')
    if (apiKey != null)
        project {
            id = '317121'
            changelog = 'A changelog can be found at https://github.com/shedaniel/cloth-api/commits'
            releaseType = 'release'
            addGameVersion '1.18.2'
            addGameVersion '1.18-Snapshot'
            addGameVersion 'Fabric'
            mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")) {
                displayName = "[Fabric $project.minecraft_version] v$project.version"
            }
            afterEvaluate {
                uploadTask.dependsOn("remapJar")
            }
        }
    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}
